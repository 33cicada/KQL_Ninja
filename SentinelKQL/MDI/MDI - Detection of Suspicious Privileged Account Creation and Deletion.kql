let timeRange = 1d;
let rapidThreshold = 3;
IdentityDirectoryEvents
| where TimeGenerated > ago(timeRange)
| where ActionType == "User Account Created" or ActionType == "User Account Disabled"
| extend UnusualChars = iff(TargetAccountDisplayName matches regex "[^a-zA-Z0-9._\\-@]", 1, 0)
| extend SingleCharacterName = iff(TargetAccountDisplayName matches regex "^[a-zA-Z]$|^[a-zA-Z]\\.[a-zA-Z]$", 1, 0)
| extend CreatorHasExpectedFormat = iff(AccountDisplayName matches regex ".*\\.(0[1-9]|1[0-9]|2[0-3]|[1-9][0-9])$", 0, 2)
| extend SelfCreation = iff(AccountDisplayName == TargetAccountDisplayName, 5, 0)
| summarize 
    AccountActionCount = count(),
    CreatedAccounts = make_set(TargetAccountDisplayName),
    UserPrincipalNames = make_set(TargetAccountUpn),
    IPAddresses = make_set(IPAddress),
    Locations = make_set(Location),
    ActionTypes = make_set(ActionType),
    MaxSelfCreation = max(SelfCreation),
    MaxUnusualChars = max(UnusualChars),
    MaxSingleCharName = max(SingleCharacterName),
    MaxCreatorFormatIssue = max(CreatorHasExpectedFormat),
    any(AccountSid), 
    any(Application), 
    any(DeviceName), 
    any(TargetDeviceName), 
    any(TargetAccountUpn), 
    any(SourceSystem), 
    any(AccountDomain) 
    by AccountDisplayName, bin(TimeGenerated, 1h)
| extend RapidAccountActions = iff(AccountActionCount >= rapidThreshold, 4, 0)
| extend AnomalyScore = (MaxUnusualChars * 2) + (MaxSingleCharName * 3) + MaxCreatorFormatIssue + MaxSelfCreation + RapidAccountActions
| extend AnomalyDetails = strcat(
    iff(MaxUnusualChars > 0, "Account(s) with unusual characters; ", ""),
    iff(MaxSingleCharName > 0, "Account(s) with single character name; ", ""),
    iff(MaxCreatorFormatIssue > 0, "Creator account lacks expected number format (.XX); ", ""),
    iff(MaxSelfCreation > 0, "Self-created account detected; ", ""),
    iff(RapidAccountActions > 0, strcat("Creator performed ", toint(AccountActionCount), " account actions in an hour; "), "")
)
| where AnomalyScore > 3
| extend CreatorIPAddresses = case(array_length(IPAddresses) > 0, IPAddresses, todynamic(""))
| project-away CreatorIPAddresses
| extend 
    Locations = iff(array_length(Locations) > 0, Locations, pack_array("")),
    Application = iff(isempty(any_Application), "", any_Application),
    DeviceId = any_DeviceName,
    DeviceName = any_DeviceName,
    TargetDeviceName = any_TargetDeviceName,
    TargetDomainName = any_AccountDomain,
    SourceDeviceName = any_SourceSystem,
    SourceDomainName = any_AccountDomain
| project 
    TimeGenerated, 
    CreatorAccount = AccountDisplayName,
    CreatorSID = any_AccountSid,
    CreatedAccounts,
    UserPrincipalNames,
    ActionTypes,
    AccountActionsCount = AccountActionCount,
    AnomalyScore,
    AnomalyDetails,
    Application,
    DeviceId,
    DeviceName,
    TargetDeviceName,
    TargetDomainName,
    SourceDeviceName,
    SourceDomainName
| sort by AnomalyScore desc, TimeGenerated desc
